CREATE PROCEDURE [dbo].[SPR_FIS_CollegeStructure]
AS
BEGIN
	SET XACT_ABORT, NOCOUNT ON;

	--Maths and English Courses
	DROP TABLE IF EXISTS #MathsEngCourses
	SELECT DISTINCT
		AcademicYear = CRS.AcademicYearID,
		ProvSpecValue = ENR.UserDefined7,
		CourseCode = ENR.UserDefined7,
		CourseTitle = CRS.Name,
		CollegeLevel1Code = COALESCE ( RTRIM ( COL.Code ), '-' ),
		CollegeLevel1Name = COALESCE ( COL.Name, '-- Unknown --' ),
		CollegeLevel2Code = COALESCE ( RTRIM ( AP.Code ), '-' ),
		CollegeLevel2Name = COALESCE ( AP.Name, '-- Unknown --' ),
		CollegeLevel3Code = COALESCE ( RTRIM ( FAC.Code ), '-' ),
		CollegeLevel3Name = COALESCE ( FAC.Name, '-- Unknown --' ),
		CollegeLevel4Code = COALESCE ( RTRIM ( TEAM.Code ), '-' ),
		CollegeLevel4Name = COALESCE ( TEAM.Name, '-- Unknown --' )
		INTO #MathsEngCourses
	FROM ProSolution.dbo.StudentDetail SD
	INNER JOIN ProSolution.dbo.Enrolment ENR
		ON ENR.StudentDetailID = SD.StudentDetailID
	INNER JOIN ProSolution.dbo.Offering CRS
		ON CRS.OfferingID = ENR.OfferingID
	INNER JOIN ProSolution.dbo.OfferingStatus STA
		ON STA.OfferingStatusID = CRS.OfferingStatusID
	LEFT JOIN ProSolution.dbo.CollegeLevel TEAM
		ON TEAM.SID = CRS.SID
	LEFT JOIN ProSolution.dbo.CollegeLevel FAC
		ON FAC.SID = TEAM.ParentSID
	LEFT JOIN ProSolution.dbo.CollegeLevel AP
		ON AP.SID = FAC.ParentSID
	LEFT JOIN ProSolution.dbo.CollegeLevel COL
		ON COL.SID = AP.ParentSID
	WHERE
		STA.CanTimetable = 1
		AND ENR.UserDefined7 IS NOT NULL
		AND COALESCE ( ENR.UserDefined7, 'X' ) <> CRS.Code
		AND ENR.UserDefined7 NOT LIKE ' %'


	--Other Provision
	SELECT DISTINCT
		AcademicYear = CRS.AcademicYearID,
		ProvSpecValue = CRS.Code,
		CourseCode = CRS.Code,
		CourseTitle = CRS.Name,
		CollegeLevel1Code = COALESCE ( RTRIM ( COL.Code ), '-' ),
		CollegeLevel1Name = COALESCE ( COL.Name, '-- Unknown --' ),
		CollegeLevel2Code = COALESCE ( RTRIM ( AP.Code ), '-' ),
		CollegeLevel2Name = COALESCE ( AP.Name, '-- Unknown --' ),
		CollegeLevel3Code = COALESCE ( RTRIM ( FAC.Code ), '-' ),
		CollegeLevel3Name = COALESCE ( FAC.Name, '-- Unknown --' ),
		CollegeLevel4Code = COALESCE ( RTRIM ( TEAM.Code ), '-' ),
		CollegeLevel4Name = COALESCE ( TEAM.Name, '-- Unknown --' )
	FROM ProSolution.dbo.Offering CRS
	INNER JOIN ProSolution.dbo.OfferingStatus STA
		ON STA.OfferingStatusID = CRS.OfferingStatusID
	LEFT JOIN ProSolution.dbo.CollegeLevel TEAM
		ON TEAM.SID = CRS.SID
	LEFT JOIN ProSolution.dbo.CollegeLevel FAC
		ON FAC.SID = TEAM.ParentSID
	LEFT JOIN ProSolution.dbo.CollegeLevel AP
		ON AP.SID = FAC.ParentSID
	LEFT JOIN ProSolution.dbo.CollegeLevel COL
		ON COL.SID = AP.ParentSID
	LEFT JOIN #MathsEngCourses MAE
		ON MAE.CourseCode = CRS.Code
		AND MAE.AcademicYear = CRS.AcademicYearID
	WHERE
		STA.CanTimetable = 1
		AND MAE.CourseCode IS NULL

	UNION ALL
	
	SELECT
		MAE.AcademicYear,
		MAE.ProvSpecValue,
		MAE.CourseCode,
		MAE.CourseTitle,
		MAE.CollegeLevel1Code,
		MAE.CollegeLevel1Name,
		MAE.CollegeLevel2Code,
		MAE.CollegeLevel2Name,
		MAE.CollegeLevel3Code,
		MAE.CollegeLevel3Name,
		MAE.CollegeLevel4Code,
		MAE.CollegeLevel4Name
	FROM #MathsEngCourses MAE
END