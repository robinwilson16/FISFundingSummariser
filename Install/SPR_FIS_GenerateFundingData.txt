CREATE PROCEDURE SPR_FIS_GenerateFundingData
	@FISDatabase NVARCHAR(50),
	@AcademicYear NVARCHAR(5),
	@ILRReturn INT,
	@IsFinalReturn BIT,
	@Split1619Funding BIT,
	@Reapportion1619FundingByAimWeighting BIT,
	@1619ALSFundingPercent DECIMAL(5,2),
	@IncludeHEAdvLoanPossIncome BIT,
	@IncludeAdvLoanBursaryIncome BIT,
	@FutureAchieveWeighting DECIMAL(5,2),
	@Mode NCHAR(1),
	@FISOutputTableLocation NVARCHAR(200),
	@FISOutputTableName NVARCHAR(200),
	@ProviderFieldCourseLocation NCHAR(1),
	@ProviderFieldParentCourseLocation NCHAR(1),
	@OverrideEngFac NVARCHAR(50),
	@OverrideMatFac NVARCHAR(50),
	@OverrideEngTeam NVARCHAR(50),
	@OverrideMatTeam NVARCHAR(50),
	@OverrideEngCostCentre NVARCHAR(50),
	@OverrideMatCostCentre NVARCHAR(50),
	@OverridePartnerFac NVARCHAR(50),
	@OverridePartnerTeam NVARCHAR(50),
	@OverridePartnerCostCentre NVARCHAR(50),
	@AnonymiseData BIT
AS
BEGIN
	SET XACT_ABORT, NOCOUNT ON;

	DECLARE @TableExists BIT = 1;
	DECLARE @FileDate DATETIME;
	DECLARE @NumExistingRecords INT = 0;
	
	DECLARE @SQLString NVARCHAR(MAX);
	DECLARE @SQLParams NVARCHAR(MAX);

	DECLARE @Message NVARCHAR(MAX);

	--DECLARE @ILRReturn int = 14 -- CHANGE THIS TO CORRECT RETURN
	--DECLARE @IsFinalReturn BIT = 1 -- IF FINAL RETURN SET THIS TO 1 - ensure only 1 return for each month is set to 1 for accurate year on year reporting

	--DECLARE @FISDatabase NVARCHAR(100) = 'ILR2223'
	--DECLARE @AcademicYear NVARCHAR(5) = '22/23'

	--DECLARE @Split1619Funding BIT = 0 --Split the funding into the 12 periods
	--DECLARE @Reapportion1619FundingByAimWeighting BIT = 0 --Reapportion the 16-19 funding by the aim prog weighting factor
	--DECLARE @1619ALSFundingPercent DECIMAL(5,2) = 0 --Deduct this percent from Tot Funding for FM25 and assign to ALS column
	--DECLARE @IncludeHEAdvLoanPossIncome BIT = 0 --HE gross fee is only achieved if at all 3 SLC census points learners remain current
	--DECLARE @IncludeAdvLoanBursaryIncome BIT = 0 --Adv loan bursary income is not income as goes to learners so may want to exclude
	--DECLARE @FutureAchieveWeighting DECIMAL(5,2) = 1 --Future ach weighting factor. If 1 then is 100% of all remaining ach monies
	--DECLARE @Mode NCHAR(1) = 'I' --I=Insert new ILR return data leaving existing data, R=Replace table
	--DECLARE @FISOutputTableLocation NVARCHAR(200) = 'IEG.dbo.'
	--DECLARE @FISOutputTableName NVARCHAR(200) = 'FIS_FundingData'
	--DECLARE @ProviderFieldCourseLocation NCHAR(1) = 'A'
	--DECLARE @ProviderFieldParentCourseLocation NCHAR(1) = 'B'
	--DECLARE @OverrideEngFac NVARCHAR(50) = NULL
	--DECLARE @OverrideMatFac NVARCHAR(50) = NULL
	--DECLARE @OverrideEngTeam NVARCHAR(50) = NULL
	--DECLARE @OverrideMatTeam NVARCHAR(50) = NULL
	--DECLARE @OverrideEngCostCentre NVARCHAR(50) = NULL
	--DECLARE @OverrideMatCostCentre NVARCHAR(50) = NULL
	--DECLARE @OverridePartnerFac NVARCHAR(50) = NULL
	--DECLARE @OverridePartnerTeam NVARCHAR(50) = NULL
	--DECLARE @OverridePartnerCostCentre NVARCHAR(50) = NULL
	--DECLARE @AnonymiseData BIT = 0 --Anonymise ILR data

	DECLARE @ILRReturnSummary NVARCHAR(200) = NULL;

	SET @Message = 'Starting Operation at ' + FORMAT ( SYSDATETIME(), 'dd/MM/yyyy HH:mm:ss' );
	RAISERROR ( @Message, 0, 1 ) WITH NOWAIT;

	SET @ILRReturnSummary = 
		'Importing ' + @AcademicYear + ' R' + FORMAT ( @ILRReturn, '00' ) + ' '
		+ CASE
			WHEN @IsFinalReturn = 1 THEN 'Final'
			ELSE 'Non-Final'
		END
		+ ' ILR Return';

	SET @Message = @ILRReturnSummary;
	RAISERROR ( @Message, 0, 1 ) WITH NOWAIT;

	SET @Message = 'Using FIS Database ' + @FISDatabase;
	RAISERROR ( @Message, 0, 1 ) WITH NOWAIT;

	SET @Message = 'Importing into ' + @FISOutputTableLocation + @FISOutputTableName;
	RAISERROR ( @Message, 0, 1 ) WITH NOWAIT;

	--Get date of new ILR Return to be imported
	IF @AcademicYear >= '19/20'
	BEGIN
		SET @SQLString = N'
			SELECT
				@FileDateOUT = SRC.DateTime
			FROM ' + @FISDatabase + '.Valid.Source SRC';
	END
	ELSE
	BEGIN
		SET @SQLString = N'
			SELECT
				@FileDateOUT = SRC.DateTime
			FROM ' + @FISDatabase + '.dbo.Valid_Source SRC';
	END

    SET @SQLParams = 
        N'@FileDateOUT DATETIME OUTPUT';

    EXECUTE sp_executesql 
        @SQLString, 
        @SQLParams, 
        @FileDateOUT = @FileDate OUTPUT;

	SET @Message = 'Date of ILR Return to be Imported: ' + FORMAT ( @FileDate, 'dd/MM/yyyy' );
	RAISERROR ( @Message, 0, 1 ) WITH NOWAIT;

	IF @Mode = 'R'
		--If mode is to replace then drop the table
		BEGIN
			SET @SQLString = N'
				DROP TABLE IF EXISTS dbo.[' + @FISOutputTableName + ']';

			SET @SQLParams = N'';

			EXECUTE sp_executesql 
				@SQLString, 
				@SQLParams;
		END
	ELSE
		--Confirm table exists already
		IF OBJECT_ID('dbo.FIS_FundingData', 'U') IS NULL 
			SET @TableExists = 0;

	IF @Mode = 'R' OR @TableExists = 0
		--If mode is to replace or table does not exist then create it
		BEGIN
			SET @SQLString = N'
				CREATE TABLE ' + @FISOutputTableLocation + '[' + @FISOutputTableName + '] (
					AcademicYear NVARCHAR(5) NULL,
                    ProviderNumber INT NULL,
					ProviderName NVARCHAR(200) NULL,
					ILRReturn NVARCHAR(3) NULL,
					IsFinalReturn INT NULL,
					ILRReturnDate DATETIME NULL,
					SQLDatabase NVARCHAR(50) NULL,
					ImportDate DATETIME NULL,
					Is1619FundingSplit INT NULL,
					ALS1619FundingPercent DECIMAL(5, 2) NULL,
					Is1619FundingReapportionedByAim INT NULL,
					InvalidLearners INT NULL,

					LearnerRef NVARCHAR(12) NULL,
					Surname NVARCHAR(100) NULL,
					Forename NVARCHAR(100) NULL,
					Sex NVARCHAR(1) NULL,
					DOB DATETIME NULL,
					Age31Aug INT NULL,
					ULN BIGINT NULL,
					NINumber NVARCHAR(9) NULL,
					Address1 NVARCHAR(50) NULL,
					Address2 NVARCHAR(50) NULL,
					Address3 NVARCHAR(50) NULL,
					Address4 NVARCHAR(50) NULL,
					PostCodeCurrent NVARCHAR(10) NULL,
					PostCodePriorToEnrol NVARCHAR(8) NULL,
					PostCodeLearningStartDate NVARCHAR(8) NULL,
					Tel NVARCHAR(18) NULL,
					Email NVARCHAR(100) NULL,
					PostCodeWardCode NVARCHAR(10) NULL,
					PostCodeWardName NVARCHAR(100) NULL,
					PostCodeDistrictCode NVARCHAR(10) NULL,
					PostCodeDistrictName NVARCHAR(50) NULL,
					PostCodeLEACode NVARCHAR(10) NULL,
					PostCodeLEAName NVARCHAR(50) NULL,
					PostCodeSourceOfFundingCode NVARCHAR(50) NULL,
					PostCodeDisadvUplift DECIMAL(6, 5) NULL,
					EmpStatusCode INT NULL,
					EmpStatusName NVARCHAR(20) NULL,
					EmpStatusAppliesDate DATETIME NULL,
					EmpStatusEmployerID INT NULL,
					LengthOfEmploy INT NULL,
					LengthOfUnemploy INT NULL,
					BenefitStatusInd INT NULL,
					SmallEmployer INT NULL,
					SelfEmployInd INT NULL,
					EmployIntensityInd INT NULL,
					IsFirstFullLevel2 INT NULL,
					IsFirstFullLevel3 INT NULL,
					PriorLevelCode INT NULL,
					PriorLevelName NVARCHAR(80) NULL,
					PriorLevelDate DATE NULL,
					EthnicityCode INT NULL,
					EthnicityName NVARCHAR(62) NULL,
					LLDDHealthProb INT NULL,
					PrimaryLLDDHealthProblemCode INT NULL,
					PrimaryLLDDHealthProblemName NVARCHAR(100) NULL,
					FAMLearnerHNS INT NULL,
					FAMLearnerEHC INT NULL,
					FAMLearnerDLA INT NULL,
					FAMLearnerLSR INT NULL,
					FAMLearnerSEN INT NULL,
					FAMLearnerNLM INT NULL,
					FAMLearnerEDF INT NULL,
					FAMLearnerMCF INT NULL,
					FAMLearnerECF INT NULL,
					FAMLearnerFME INT NULL,
					DestinationProgressionType NVARCHAR(3) NULL,
					DestinationProgressionNumber INT NULL,
					DestinationProgressionCode NVARCHAR(10) NULL,
					DestinationProgressionName NVARCHAR(255) NULL,
					DestinationProgressionStartDate DATETIME NULL,
					DestinationProgressionEndDate DATETIME NULL,
					DestinationProgressionCollectionDate DATETIME NULL,
	'
    
    SET @SQLString += 
        N'
					SoftwareSupplierAimID NVARCHAR(36) NULL,
					FundLine NVARCHAR(100) NULL,
					FundLineSummary NVARCHAR(100) NULL,
                    FundLineDetail NVARCHAR(100) NULL,
					FundModel INT NULL,
					HEFullPartTime NVARCHAR(50) NULL,
					SortOrder INT NULL,
					IsFunded INT NULL,
					IsPre42DayWdrAllYr INT NULL,
					IsXfrAllYr INT NULL,
					IsStartAllYr INT NULL,
					IsPost42DayWdrAllYr INT NULL,
					IsCompAllYr INT NULL,
					IsAchAllYr INT NULL,
					IsPre42DayWdrCurYr INT NULL,
					IsXfrCurYr INT NULL,
					IsStartCurYr INT NULL,
					IsPost42DayWdrCurYr INT NULL,
					IsCompCurYr INT NULL,
					IsAchCurYr INT NULL,
					IsHighGrade INT NULL,

					IsMainAim INT NULL,
					IsMainAimInFundModel INT NULL,
					IsAdvLearnLoan INT NULL,
					IsAdvLearnLoanBursary INT NULL,
					RateBand NVARCHAR(50) NULL,
					RateBandOrder INT NULL,
					GCSEMathsGrade NVARCHAR(4) NULL,
					GCSEEnglishGrade NVARCHAR(4) NULL,
					CofMaths NVARCHAR(100) NULL,
					CofEnglish NVARCHAR(100) NULL,
					CofMathsMet INT NULL,
					CofEnglishMet INT NULL,
					CofMet INT NULL,
					Block1DisadvUplift DECIMAL(10, 5) NULL,
					Block2DisadvElements DECIMAL(10, 5) NULL,
					ProgWeightingFactor DECIMAL(10, 5) NULL,
					ThresholdDays INT NULL,
					FAMLearningDeliverySOF NVARCHAR(5) NULL,
					FAMLearningDeliveryFFI NVARCHAR(5) NULL,
					FAMLearningDeliveryEEF NVARCHAR(5) NULL,
					FAMLearningDeliveryRES NVARCHAR(5) NULL,
					FAMLearningDeliveryLSF NVARCHAR(5) NULL,
					FAMLearningDeliveryADL NVARCHAR(5) NULL,
					FAMLearningDeliveryALB NVARCHAR(5) NULL,
					FAMLearningDeliveryASL NVARCHAR(5) NULL,
					FAMLearningDeliveryFLN NVARCHAR(5) NULL,
					FAMLearningDeliveryLDM NVARCHAR(5) NULL,
					FAMLearningDeliveryDAM NVARCHAR(5) NULL,
					FAMLearningDeliveryACT NVARCHAR(5) NULL,
					FAMLearningDeliveryACL NVARCHAR(5) NULL,
					FAMLearningDeliveryAFL NVARCHAR(5) NULL,
	'
    
    SET @SQLString += 
        N'
					PostCodeDelivery NVARCHAR(10) NULL,
					CampusID NVARCHAR(20) NULL,

					ParentCollegeLevel1Code NVARCHAR(20) NULL,
					ParentCollegeLevel1Name NVARCHAR(150) NULL,
					ParentCollegeLevel2Code NVARCHAR(20) NULL,
					ParentCollegeLevel2Name NVARCHAR(150) NULL,
					ParentCollegeLevel3Code NVARCHAR(20) NULL,
					ParentCollegeLevel3Name NVARCHAR(150) NULL,
					ParentCollegeLevel4Code NVARCHAR(20) NULL,
					ParentCollegeLevel4Name NVARCHAR(150) NULL,

					CollegeLevel1Code NVARCHAR(20) NULL,
					CollegeLevel1Name NVARCHAR(150) NULL,
					CollegeLevel2Code NVARCHAR(20) NULL,
					CollegeLevel2Name NVARCHAR(150) NULL,
					CollegeLevel3Code NVARCHAR(20) NULL,
					CollegeLevel3Name NVARCHAR(150) NULL,
					CollegeLevel4Code NVARCHAR(20) NULL,
					CollegeLevel4Name NVARCHAR(150) NULL,

					OriginalCollegeLevel1Code NVARCHAR(20) NULL,
					OriginalCollegeLevel1Name NVARCHAR(150) NULL,
					OriginalCollegeLevel2Code NVARCHAR(20) NULL,
					OriginalCollegeLevel2Name NVARCHAR(150) NULL,
					OriginalCollegeLevel3Code NVARCHAR(20) NULL,
					OriginalCollegeLevel3Name NVARCHAR(150) NULL,
					OriginalCollegeLevel4Code NVARCHAR(20) NULL,
					OriginalCollegeLevel4Name NVARCHAR(150) NULL,
					
					MainAimCollegeLevel1Code NVARCHAR(20) NULL,
					MainAimCollegeLevel1Name NVARCHAR(150) NULL,
					MainAimCollegeLevel2Code NVARCHAR(20) NULL,
					MainAimCollegeLevel2Name NVARCHAR(150) NULL,
					MainAimCollegeLevel3Code NVARCHAR(20) NULL,
					MainAimCollegeLevel3Name NVARCHAR(150) NULL,
					MainAimCollegeLevel4Code NVARCHAR(20) NULL,
					MainAimCollegeLevel4Name NVARCHAR(150) NULL,

					SSA1Code NVARCHAR(5) NULL,
					SSA1Title NVARCHAR(255) NULL,
					SSA2Code NVARCHAR(5) NULL,
					SSA2Title NVARCHAR(255) NULL,
					CourseCode NVARCHAR(20) NULL,
					CourseTitle NVARCHAR(255) NULL,

					AimSeqNumber INT NULL,
					LearnerAndAimRowNum INT NULL,
					AimType INT NULL,

					LearningAimCode NVARCHAR(8) NULL,
					LearningAimTitle NVARCHAR(254) NULL,
					LearningAimTypeCode NVARCHAR(4) NULL,
					LearningAimTypeName NVARCHAR(150) NULL,

					NVQLevelCode NCHAR(1) NULL,
					NVQLevelName NVARCHAR(50) NULL,
					NVQLevelOrder DECIMAL(10, 5) NULL,
					IsMathsAndEnglish NCHAR(1) NULL,
					MainAimNVQLevelCode NCHAR(1) NULL,
					MainAimNVQLevelName NVARCHAR(50) NULL,
					MainAimNVQLevelOrder DECIMAL(10, 5) NULL,
					MainAimIsMathsAndEnglish NCHAR(1) NULL,

					StartDate DATETIME NULL,
					OriginalStartDate DATETIME NULL,
					PlannedEndDate DATETIME NULL,
					ActualEndDate DATETIME NULL,
					AchievementDate DATETIME NULL,
					AchievementYear NVARCHAR(5) NULL,
					PlannedEndYear NVARCHAR(5) NULL,
					ActualEndYear NVARCHAR(5) NULL,
					ReportingYear NVARCHAR(5) NULL,
					HybridEndYear NVARCHAR(5) NULL,

					PlanLearnHours INT NULL,
					PlanEEPHours INT NULL,
					FM25PLHAim INT NULL,
					FM25EEPAim INT NULL,
					FM25PLHAllAims INT NULL,
					FM25EEPAllAims INT NULL,
					IsPartTime NCHAR(1) NULL,

					CompletionStatusCode INT NULL,
					CompletionStatusName NVARCHAR(20) NULL,
					OutcomeCode INT NULL,
					OutcomeName NVARCHAR(20) NULL,
					WithdrawReason NVARCHAR(2) NULL,
					Grade NVARCHAR(6) NULL,
					AwardBody NVARCHAR(20) NULL,

					ProgType INT NULL,
					StandardCode INT NULL,
					StandardName NVARCHAR(750) NULL,
					FrameworkCode INT NULL,
					FrameworkName NVARCHAR(150) NULL,
					PathwayCode INT NULL,
					PathwayName NVARCHAR(150) NULL,
					OffTheJobActualHours INT NULL,
					EmployerID INT NULL,
					EmployerName NVARCHAR(100) NULL,
					PartnerCode INT NULL,
					PartnerName NVARCHAR(100) NULL,
					
					AimValue DECIMAL(10, 5) NULL,
					ProgWeightFactor NCHAR(1) NULL,
					UnweightFundRate DECIMAL(10, 5) NULL,
					WeightFundRate DECIMAL(10, 5) NULL,
					CoFundingIndicator NVARCHAR(10) NULL,
					IsCarryIn INT NULL,
					HEQualEnt3 NVARCHAR(3) NULL,
					HESoc2000 INT NULL,
					HESec INT NULL,
					HEUCASAppID NVARCHAR(9) NULL,
					HETypeYr INT NULL,
					HEModeStu INT NULL,
					HEFundLev INT NULL,
					HEFundComp INT NULL,
					HEStuLoad DECIMAL(4, 1) NULL,
					HEYearStu INT NULL,
					HEMajorSourceStuFee INT NULL,
					HEPercentageNotTaught DECIMAL(4, 1) NULL,
					HEPercentTaughtFirstLDCS DECIMAL(4, 1) NULL,
					HEPercentTaughtSecondLDCS DECIMAL(4, 1) NULL,
					HEPercentTaughtThirdLDCS DECIMAL(4, 1) NULL,
					HESpecialFeeIndicator INT NULL,
					HENetTuitionFee INT NULL,
					HEGrossTuitionFee INT NULL,
					HEDomicile NVARCHAR(2) NULL,
					HEELQ INT NULL,
					HEPostCode NVARCHAR(8) NULL,
					EnrolmentID NVARCHAR(20) NULL,
					FM25TotFund DECIMAL(10, 5) NULL,
	'
    
    SET @SQLString += 
        N'
					NumPlannedOnProgPayments INT NULL,
					NumOutstandingOnProgPayments INT NULL,
					AchieveElement DECIMAL(10, 5) NULL,
					NonGovCont DECIMAL(10, 5) NULL,
					PropFundRemain DECIMAL(10, 5) NULL,
					PropFundRemainAch DECIMAL(10, 5) NULL,
					CarryInOnProg DECIMAL(10, 5) NULL,
					CarryInYr1R01 DECIMAL(10, 5) NULL,
					CarryInYr1R02 DECIMAL(10, 5) NULL,
					CarryInYr1R03 DECIMAL(10, 5) NULL,
					CarryInYr1R04 DECIMAL(10, 5) NULL,
					CarryInYr1R05 DECIMAL(10, 5) NULL,
					CarryInYr1R06 DECIMAL(10, 5) NULL,
					CarryInYr1R07 DECIMAL(10, 5) NULL,
					CarryInYr1R08 DECIMAL(10, 5) NULL,
					CarryInYr1R09 DECIMAL(10, 5) NULL,
					CarryInYr1R10 DECIMAL(10, 5) NULL,
					CarryInYr1R11 DECIMAL(10, 5) NULL,
					CarryInYr1R12 DECIMAL(10, 5) NULL,
					CarryInYr2R01 DECIMAL(10, 5) NULL,
					CarryInYr2R02 DECIMAL(10, 5) NULL,
					CarryInYr2R03 DECIMAL(10, 5) NULL,
					CarryInYr2R04 DECIMAL(10, 5) NULL,
					CarryInYr2R05 DECIMAL(10, 5) NULL,
					CarryInYr2R06 DECIMAL(10, 5) NULL,
					CarryInYr2R07 DECIMAL(10, 5) NULL,
					CarryInYr2R08 DECIMAL(10, 5) NULL,
					CarryInYr2R09 DECIMAL(10, 5) NULL,
					CarryInYr2R10 DECIMAL(10, 5) NULL,
					CarryInYr2R11 DECIMAL(10, 5) NULL,
					CarryInYr2R12 DECIMAL(10, 5) NULL,
    '
    
    SET @SQLString += 
        N'
					OnProgPaymentToPeriod DECIMAL(10, 5) NULL,
					LearnSuppPaymentToPeriod DECIMAL(10, 5) NULL,
					AchCompPaymentToPeriod DECIMAL(10, 5) NULL,
					BalancePaymentToPeriod DECIMAL(10, 5) NULL,
					EmpOutcomePayToPeriod DECIMAL(10, 5) NULL,
					OtherPaymentToPeriod DECIMAL(10, 5) NULL,
					TotFundToPeriod DECIMAL(10, 5) NULL,

					OnProgPayment6Mon DECIMAL(10, 5) NULL,
					LearnSuppPayment6Mon DECIMAL(10, 5) NULL,
					AchCompPayment6Mon DECIMAL(10, 5) NULL,
					BalancePayment6Mon DECIMAL(10, 5) NULL,
					EmpOutcomePay6Mon DECIMAL(10, 5) NULL,
					OtherPayment6Mon DECIMAL(10, 5) NULL,
					TotFund6Mon DECIMAL(10, 5) NULL,

					OnProgPaymentYrEnd DECIMAL(10, 5) NULL,
					LearnSuppPaymentYrEnd DECIMAL(10, 5) NULL,
					AchCompPaymentYrEnd DECIMAL(10, 5) NULL,
					BalancePaymentYrEnd DECIMAL(10, 5) NULL,
					EmpOutcomePayYrEnd DECIMAL(10, 5) NULL,
					OtherPaymentYrEnd DECIMAL(10, 5) NULL,
					TotFundYrEnd DECIMAL(10, 5) NULL,
					TotFundYrEndNotApportioned DECIMAL(10, 5) NULL,

					FutureAchievePayment DECIMAL(10, 5) NULL,
					FutureAchieveWeighting DECIMAL(5, 2) NULL,
					FutureAchievePaymentWeighted DECIMAL(10, 5) NULL,

					AppStandardPriceEpisodeIdentifier NVARCHAR(25) NULL,
					AppStandardTNP1 DECIMAL(12, 5) NULL,
					AppStandardTNP2 DECIMAL(12, 5) NULL,
					AppStandardTNP3 DECIMAL(12, 5) NULL,
					AppStandardTNP4 DECIMAL(12, 5) NULL,
					AppStandardEpisodeStartDate DATE NULL,
					AppStandardPriceEpisode1618FrameworkUpliftRemainingAmount DECIMAL(12, 5) NULL,
					AppStandardPriceEpisode1618FrameworkUpliftTotPrevEarnings DECIMAL(12, 5) NULL,
					AppStandardPriceEpisode1618FUBalValue DECIMAL(12, 5) NULL,
					AppStandardPriceEpisode1618FUMonthInstValue DECIMAL(12, 5) NULL,
					AppStandardPriceEpisode1618FUTotEarnings DECIMAL(12, 5) NULL,
					AppStandardPriceEpisodeUpperBandLimit DECIMAL(12, 5) NULL,
					AppStandardPriceEpisodePlannedEndDate DATE NULL,
					AppStandardPriceEpisodeActualEndDate DATE NULL,
					AppStandardPriceEpisodeActualEndDateIncEPA DATE NULL,
					AppStandardPriceEpisodeTotalTNPPrice DECIMAL(12, 5) NULL,
					AppStandardPriceEpisodeUpperLimitAdjustment DECIMAL(12, 5) NULL,
					AppStandardPriceEpisodePlannedInstalments INT NULL,
					AppStandardPriceEpisodeActualInstalments INT NULL,
					AppStandardPriceEpisodeCompletionElement DECIMAL(12, 5) NULL,
					AppStandardPriceEpisodePreviousEarnings DECIMAL(12, 5) NULL,
					AppStandardPriceEpisodeInstalmentValue DECIMAL(12, 5) NULL,
					AppStandardPriceEpisodeTotalEarnings DECIMAL(12, 5) NULL,
					AppStandardPriceEpisodeCompleted BIT NULL,
					AppStandardPriceEpisodeRemainingTNPAmount DECIMAL(12, 5) NULL,
					AppStandardPriceEpisodeRemainingAmountWithinUpperLimit DECIMAL(12, 5) NULL,
					AppStandardPriceEpisodeCappedRemainingTNPAmount DECIMAL(12, 5) NULL,
					AppStandardPriceEpisodeExpectedTotalMonthlyValue DECIMAL(12, 5) NULL,
					AppStandardPriceEpisodeAimSeqNumber INT NULL,
					AppStandardPriceEpisodeApplic1618FrameworkUpliftCompElement DECIMAL(12, 5) NULL,
					AppStandardPriceEpisodeFundLineType NVARCHAR(100) NULL,
					AppStandardEpisodeEffectiveTNPStartDate DATE NULL,
					AppStandardPriceEpisodeFirstAdditionalPaymentThresholdDate DATE NULL,
					AppStandardPriceEpisodeSecondAdditionalPaymentThresholdDate DATE NULL,
					AppStandardPriceEpisodeContractType NVARCHAR(50) NULL,
					AppStandardPriceEpisodePreviousEarningsSameProvider DECIMAL(12, 5) NULL,
					AppStandardPriceEpisodeTotalPMRs DECIMAL(12, 5) NULL,
					AppStandardPriceEpisodeCumulativePMRs DECIMAL(12, 5) NULL,
					AppStandardPriceEpisodeCompExemCode INT NULL,
					AppStandardPriceEpisodeLearnerAdditionalPaymentThresholdDate DATE NULL,
					AppStandardPriceEpisodeRedStartDate DATE NULL,
					AppStandardPriceEpisodeRedStatusCode INT NULL,
	'
    
    SET @SQLString += 
        N'
					OnProgPaymentP01 DECIMAL(10, 5) NULL,
					LearnSuppPaymentP01 DECIMAL(10, 5) NULL,
					AchCompPaymentP01 DECIMAL(10, 5) NULL,
					BalancePaymentP01 DECIMAL(10, 5) NULL,
					EmpOutcomePayP01 DECIMAL(10, 5) NULL,
					OtherPaymentP01 DECIMAL(10, 5) NULL,
					TotFundP01 DECIMAL(10, 5) NULL,

					OnProgPaymentP02 DECIMAL(10, 5) NULL,
					LearnSuppPaymentP02 DECIMAL(10, 5) NULL,
					AchCompPaymentP02 DECIMAL(10, 5) NULL,
					BalancePaymentP02 DECIMAL(10, 5) NULL,
					EmpOutcomePayP02 DECIMAL(10, 5) NULL,
					OtherPaymentP02 DECIMAL(10, 5) NULL,
					TotFundP02 DECIMAL(10, 5) NULL,

					OnProgPaymentP03 DECIMAL(10, 5) NULL,
					LearnSuppPaymentP03 DECIMAL(10, 5) NULL,
					AchCompPaymentP03 DECIMAL(10, 5) NULL,
					BalancePaymentP03 DECIMAL(10, 5) NULL,
					EmpOutcomePayP03 DECIMAL(10, 5) NULL,
					OtherPaymentP03 DECIMAL(10, 5) NULL,
					TotFundP03 DECIMAL(10, 5) NULL,

					OnProgPaymentP04 DECIMAL(10, 5) NULL,
					LearnSuppPaymentP04 DECIMAL(10, 5) NULL,
					AchCompPaymentP04 DECIMAL(10, 5) NULL,
					BalancePaymentP04 DECIMAL(10, 5) NULL,
					EmpOutcomePayP04 DECIMAL(10, 5) NULL,
					OtherPaymentP04 DECIMAL(10, 5) NULL,
					TotFundP04 DECIMAL(10, 5) NULL,

					OnProgPaymentP05 DECIMAL(10, 5) NULL,
					LearnSuppPaymentP05 DECIMAL(10, 5) NULL,
					AchCompPaymentP05 DECIMAL(10, 5) NULL,
					BalancePaymentP05 DECIMAL(10, 5) NULL,
					EmpOutcomePayP05 DECIMAL(10, 5) NULL,
					OtherPaymentP05 DECIMAL(10, 5) NULL,
					TotFundP05 DECIMAL(10, 5) NULL,

					OnProgPaymentP06 DECIMAL(10, 5) NULL,
					LearnSuppPaymentP06 DECIMAL(10, 5) NULL,
					AchCompPaymentP06 DECIMAL(10, 5) NULL,
					BalancePaymentP06 DECIMAL(10, 5) NULL,
					EmpOutcomePayP06 DECIMAL(10, 5) NULL,
					OtherPaymentP06 DECIMAL(10, 5) NULL,
					TotFundP06 DECIMAL(10, 5) NULL,

					OnProgPaymentP07 DECIMAL(10, 5) NULL,
					LearnSuppPaymentP07 DECIMAL(10, 5) NULL,
					AchCompPaymentP07 DECIMAL(10, 5) NULL,
					BalancePaymentP07 DECIMAL(10, 5) NULL,
					EmpOutcomePayP07 DECIMAL(10, 5) NULL,
					OtherPaymentP07 DECIMAL(10, 5) NULL,
					TotFundP07 DECIMAL(10, 5) NULL,

					OnProgPaymentP08 DECIMAL(10, 5) NULL,
					LearnSuppPaymentP08 DECIMAL(10, 5) NULL,
					AchCompPaymentP08 DECIMAL(10, 5) NULL,
					BalancePaymentP08 DECIMAL(10, 5) NULL,
					EmpOutcomePayP08 DECIMAL(10, 5) NULL,
					OtherPaymentP08 DECIMAL(10, 5) NULL,
					TotFundP08 DECIMAL(10, 5) NULL,

					OnProgPaymentP09 DECIMAL(10, 5) NULL,
					LearnSuppPaymentP09 DECIMAL(10, 5) NULL,
					AchCompPaymentP09 DECIMAL(10, 5) NULL,
					BalancePaymentP09 DECIMAL(10, 5) NULL,
					EmpOutcomePayP09 DECIMAL(10, 5) NULL,
					OtherPaymentP09 DECIMAL(10, 5) NULL,
					TotFundP09 DECIMAL(10, 5) NULL,

					OnProgPaymentP10 DECIMAL(10, 5) NULL,
					LearnSuppPaymentP10 DECIMAL(10, 5) NULL,
					AchCompPaymentP10 DECIMAL(10, 5) NULL,
					BalancePaymentP10 DECIMAL(10, 5) NULL,
					EmpOutcomePayP10 DECIMAL(10, 5) NULL,
					OtherPaymentP10 DECIMAL(10, 5) NULL,
					TotFundP10 DECIMAL(10, 5) NULL,

					OnProgPaymentP11 DECIMAL(10, 5) NULL,
					LearnSuppPaymentP11 DECIMAL(10, 5) NULL,
					AchCompPaymentP11 DECIMAL(10, 5) NULL,
					BalancePaymentP11 DECIMAL(10, 5) NULL,
					EmpOutcomePayP11 DECIMAL(10, 5) NULL,
					OtherPaymentP11 DECIMAL(10, 5) NULL,
					TotFundP11 DECIMAL(10, 5) NULL,

					OnProgPaymentP12 DECIMAL(10, 5) NULL,
					LearnSuppPaymentP12 DECIMAL(10, 5) NULL,
					AchCompPaymentP12 DECIMAL(10, 5) NULL,
					BalancePaymentP12 DECIMAL(10, 5) NULL,
					EmpOutcomePayP12 DECIMAL(10, 5) NULL,
					OtherPaymentP12 DECIMAL(10, 5) NULL,
					TotFundP12 DECIMAL(10, 5) NULL
				)';

			SET @SQLParams = N'';

            EXECUTE sp_executesql 
                @SQLString, 
                @SQLParams;

			SET @SQLString = N'
				CREATE CLUSTERED INDEX CX_FIS_FundingData ON ' + @FISOutputTableLocation + '[' + @FISOutputTableName + '] ( AcademicYear, ILRReturn, ILRReturnDate )';

			SET @SQLParams = N'';

            EXECUTE sp_executesql 
                @SQLString, 
                @SQLParams;

			SET @Message = 'Created ' + @FISOutputTableName + ' Table';
			RAISERROR ( @Message, 0, 1 ) WITH NOWAIT;
		END
	ELSE
		--If mode is to insert instead of replace then see if this ILR has already been imported
		BEGIN
			SET @Message = 'Inserting into Existing ' + @FISOutputTableName + ' Table';
			RAISERROR ( @Message, 0, 1 ) WITH NOWAIT;

			SET @SQLString = N'
				SELECT
					@NumExistingRecordsOUT = SUM ( 1 )
				FROM ' + @FISOutputTableLocation + '[' + @FISOutputTableName + '] FD
				WHERE
					FD.AcademicYear = @AcademicYear
					AND FD.ILRReturnDate = @FileDate';

			SET @SQLParams = 
				N'@AcademicYear NVARCHAR(5),
				@FileDate DATETIME,
				@NumExistingRecordsOUT INT OUTPUT';

			EXECUTE sp_executesql 
				@SQLString, 
				@SQLParams, 
				@AcademicYear = @AcademicYear, 
				@FileDate = @FileDate,
				@NumExistingRecordsOUT = @NumExistingRecords OUTPUT;

			IF @NumExistingRecords > 0
				--If same ILR has already been imported then delete it
				BEGIN
                    SET @SQLString = N'
                        DELETE FD
						FROM ' + @FISOutputTableLocation + '[' + @FISOutputTableName + '] FD
						WHERE
							FD.AcademicYear = @AcademicYear
							AND FD.ILRReturnDate = @FileDate';

                    SET @SQLParams = 
						N'@AcademicYear NVARCHAR(5),
						@FileDate DATETIME';

                    EXECUTE sp_executesql 
                        @SQLString, 
                        @SQLParams,
						@AcademicYear = @AcademicYear,
						@FileDate = @FileDate;

					SET @Message = 'Deleting ' + CAST ( @NumExistingRecords AS NVARCHAR(10) ) + ' From ' + @FISOutputTableName + ' Table';
					RAISERROR ( @Message, 0, 1 ) WITH NOWAIT;
				END
		END

	--Finally insert new data into table
	SET @SQLString = N'
		EXEC SPR_FIS_FundingData_' + REPLACE ( @AcademicYear, '/', '' ) + '
			@FISDatabase,
			@AcademicYear,
			@ILRReturn,
			@IsFinalReturn,
			@Split1619Funding,
			@Reapportion1619FundingByAimWeighting,
            @1619ALSFundingPercent,
			@IncludeHEAdvLoanPossIncome,
			@IncludeAdvLoanBursaryIncome,
			@FutureAchieveWeighting,
            @FISOutputTableLocation,
			@FISOutputTableName,
			@ProviderFieldCourseLocation,
			@ProviderFieldParentCourseLocation,
			@OverrideEngFac,
			@OverrideMatFac,
			@OverrideEngTeam,
			@OverrideMatTeam,
			@OverrideEngCostCentre,
			@OverrideMatCostCentre,
			@OverridePartnerFac,
			@OverridePartnerTeam,
			@OverridePartnerCostCentre';

    SET @SQLParams = 
        N'@FISDatabase NVARCHAR(50),
        @AcademicYear NVARCHAR(5),
    	@ILRReturn INT,
    	@IsFinalReturn BIT,
    	@Split1619Funding BIT,
		@Reapportion1619FundingByAimWeighting BIT,
        @1619ALSFundingPercent DECIMAL(5,2),
		@IncludeHEAdvLoanPossIncome BIT,
		@IncludeAdvLoanBursaryIncome BIT,
    	@FutureAchieveWeighting DECIMAL(5,2),
        @FISOutputTableLocation NVARCHAR(200),
		@FISOutputTableName NVARCHAR(200),
		@ProviderFieldCourseLocation CHAR(1),
		@ProviderFieldParentCourseLocation CHAR(1),
    	@OverrideEngFac NVARCHAR(50),
    	@OverrideMatFac NVARCHAR(50),
    	@OverrideEngTeam NVARCHAR(50),
    	@OverrideMatTeam NVARCHAR(50),
    	@OverrideEngCostCentre NVARCHAR(50),
    	@OverrideMatCostCentre NVARCHAR(50),
    	@OverridePartnerFac NVARCHAR(50),
    	@OverridePartnerTeam NVARCHAR(50),
    	@OverridePartnerCostCentre NVARCHAR(50)';
    
	--SELECT @SQLString AS [processing-instruction(x)] FOR XML PATH('')

    EXECUTE sp_executesql 
        @SQLString, 
        @SQLParams, 
        @FISDatabase = @FISDatabase,
        @AcademicYear = @AcademicYear,
    	@ILRReturn = @ILRReturn,
    	@IsFinalReturn = @IsFinalReturn,
    	@Split1619Funding = @Split1619Funding,
		@Reapportion1619FundingByAimWeighting = @Reapportion1619FundingByAimWeighting,
        @1619ALSFundingPercent = @1619ALSFundingPercent,
		@IncludeHEAdvLoanPossIncome = @IncludeHEAdvLoanPossIncome,
		@IncludeAdvLoanBursaryIncome = @IncludeAdvLoanBursaryIncome,
    	@FutureAchieveWeighting = @FutureAchieveWeighting,
        @FISOutputTableLocation = @FISOutputTableLocation,
		@FISOutputTableName = @FISOutputTableName,
		@ProviderFieldCourseLocation = @ProviderFieldCourseLocation,
		@ProviderFieldParentCourseLocation = @ProviderFieldParentCourseLocation,
    	@OverrideEngFac = @OverrideEngFac,
    	@OverrideMatFac = @OverrideMatFac,
    	@OverrideEngTeam = @OverrideEngTeam,
    	@OverrideMatTeam = @OverrideMatTeam,
    	@OverrideEngCostCentre = @OverrideEngCostCentre,
    	@OverrideMatCostCentre = @OverrideMatCostCentre,
    	@OverridePartnerFac = @OverridePartnerFac,
    	@OverridePartnerTeam = @OverridePartnerTeam,
    	@OverridePartnerCostCentre = @OverridePartnerCostCentre;

	SET @Message = @AcademicYear + ' ILR Imported'
	RAISERROR ( @Message, 0, 1 ) WITH NOWAIT;

	--Anonymise the ILR data if required
	IF @AnonymiseData = 1
	BEGIN
		DECLARE @AnonName VARCHAR(250) = 'Anonymous'
		DECLARE @AnonAddress VARCHAR(250) = 'Anonymous'
		DECLARE @AnonPostCode VARCHAR(250) = 'ZZ99 9ZZ'
		DECLARE @AnonTel VARCHAR(250) = '000000000000000'
		DECLARE @AnonEmail VARCHAR(250) = 'anon@anon.com'

		SET @SQLString = N'
			UPDATE FD
			SET
				Surname = CASE WHEN FD.Surname IS NOT NULL THEN @AnonName ELSE NULL END,
				Forename = CASE WHEN FD.Forename IS NOT NULL THEN @AnonName ELSE NULL END,
				Address1 = CASE WHEN FD.Address1 IS NOT NULL THEN @AnonAddress ELSE NULL END,
				Address2 = CASE WHEN FD.Address2 IS NOT NULL THEN @AnonAddress ELSE NULL END,
				Address3 = CASE WHEN FD.Address3 IS NOT NULL THEN @AnonAddress ELSE NULL END,
				Address4 = CASE WHEN FD.Address4 IS NOT NULL THEN @AnonAddress ELSE NULL END,
				PostCodeCurrent = CASE WHEN FD.PostCodeCurrent IS NOT NULL THEN @AnonPostCode ELSE NULL END,
				PostCodePriorToEnrol = CASE WHEN FD.PostCodePriorToEnrol IS NOT NULL THEN @AnonPostCode ELSE NULL END,
				PostCodeLearningStartDate = CASE WHEN FD.PostCodeLearningStartDate IS NOT NULL THEN @AnonPostCode ELSE NULL END,
				Tel = CASE WHEN FD.Tel IS NOT NULL THEN @AnonTel ELSE NULL END,
				Email = CASE WHEN FD.Email IS NOT NULL THEN @AnonEmail ELSE NULL END,
				PostCodeDelivery = CASE WHEN FD.PostCodeDelivery IS NOT NULL THEN @AnonPostCode ELSE NULL END
			FROM ' + @FISOutputTableLocation + '[' + @FISOutputTableName + '] FD
			WHERE
				FD.AcademicYear = @AcademicYear
				AND FD.ILRReturnDate = @FileDate';

		SET @SQLParams = 
			N'@AcademicYear NVARCHAR(5),
			@FileDate DATETIME,
			@AnonName VARCHAR(250),
			@AnonAddress VARCHAR(250),
			@AnonPostCode VARCHAR(250),
			@AnonTel VARCHAR(250),
			@AnonEmail VARCHAR(250)';

		EXECUTE sp_executesql 
			@SQLString, 
			@SQLParams,
			@AcademicYear = @AcademicYear,
			@FileDate = @FileDate,
			@AnonName = @AnonName,
			@AnonAddress = @AnonAddress,
			@AnonPostCode = @AnonPostCode,
			@AnonTel = @AnonTel,
			@AnonEmail = @AnonEmail;

		SET @Message = @AcademicYear + ' ILR Anonymised'
		RAISERROR ( @Message, 0, 1 ) WITH NOWAIT;
	END

	SET @Message = 'Finishing Operation at ' + FORMAT ( SYSDATETIME(), 'dd/MM/yyyy HH:mm:ss' );
	RAISERROR ( @Message, 0, 1 ) WITH NOWAIT;
END