CREATE PROCEDURE SPR_FIS_GenerateFundingData
	@FISDatabase NVARCHAR(50),
	@AcademicYear NVARCHAR(5),
	@ILRReturn INT,
	@IsFinalReturn BIT,
	@Split1619Funding BIT,
	@Reapportion1619FundingByAimWeighting BIT,
	@1619ALSFundingPercent DECIMAL(5,2),
	@IncludeHEAdvLoanPossIncome BIT,
	@IncludeAdvLoanBursaryIncome BIT,
	@FutureAchieveWeighting DECIMAL(5,2),
	@Mode NCHAR(1),
	@FISOutputTableLocation NVARCHAR(200),
	@ProviderFieldFacLocation NCHAR(1),
	@ProviderFieldTeamLocation NCHAR(1),
	@ProviderFieldCostCentreLocation NCHAR(1),
	@ProviderFieldCourseLocation NCHAR(1),
	@ProviderFieldParentCourseLocation NCHAR(1),
	@OverrideEngFac NVARCHAR(50),
	@OverrideMatFac NVARCHAR(50),
	@OverrideEngTeam NVARCHAR(50),
	@OverrideMatTeam NVARCHAR(50),
	@OverrideEngCostCentre NVARCHAR(50),
	@OverrideMatCostCentre NVARCHAR(50),
	@OverridePartnerFac NVARCHAR(50),
	@OverridePartnerTeam NVARCHAR(50),
	@OverridePartnerCostCentre NVARCHAR(50),
	@AnonymiseData BIT
AS
BEGIN
	SET NOCOUNT ON;
	DECLARE @TableExists BIT = 1;
	DECLARE @FileDate DATETIME;
	DECLARE @NumExistingRecords INT = 0;
	
	DECLARE @SQLString NVARCHAR(MAX);
	DECLARE @SQLParams NVARCHAR(MAX);

	--DECLARE @FISDatabase NVARCHAR(100) = 'Hegel.FIS_CY'
	--DECLARE @AcademicYear NVARCHAR(5) = '18/19'
	--DECLARE @ILRReturn int = 11
	--DECLARE @IsFinalReturn BIT = 1
	--DECLARE @Split1619Funding BIT = 0 --Split the funding into the 12 periods
	--DECLARE @Reapportion1619FundingByAimWeighting BIT = 0 --Reapportion the 16-19 funding by the aim prog weighting factor
	--DECLARE @1619ALSFundingPercent DECIMAL(5,2) = 0.07 --Deduct this percent from Tot Funding and assign to ALS column
	--DECLARE @IncludeHEAdvLoanPossIncome BIT = 1 --HE gross fee is only achieved if at all 3 SLC census points learners remain current
	--DECLARE @IncludeAdvLoanBursaryIncome BIT = 0 --Adv loan bursary income is not income as goes to learners so may want to exclude
	--DECLARE @FutureAchieveWeighting DECIMAL(5,2) = 1 --Future ach weighting factor. If 1 then is 100% of all remaining ach monies
	--DECLARE @Mode NCHAR(1) = 'R' --I=Insert new ILR return data leaving existing data, R=Replace table
	--DECLARE @FISOutputTableLocation NVARCHAR(200) = 'wlc.dbo.'
	--DECLARE @ProviderFieldFacLocation NCHAR(1) = 'B'
	--DECLARE @ProviderFieldTeamLocation NCHAR(1) = 'C'
	--DECLARE @ProviderFieldCostCentreLocation NCHAR(1) = 'D'
	--DECLARE @ProviderFieldCourseLocation NCHAR(1) = 'A'
	--DECLARE @ProviderFieldParentCourseLocation NCHAR(1) = 'B'
	--DECLARE @OverrideEngFac NVARCHAR(50) = '14'
	--DECLARE @OverrideMatFac NVARCHAR(50) = '14'
	--DECLARE @OverrideEngTeam NVARCHAR(50) = '1410'
	--DECLARE @OverrideMatTeam NVARCHAR(50) = '1420'
	--DECLARE @OverrideEngCostCentre NVARCHAR(50) = NULL
	--DECLARE @OverrideMatCostCentre NVARCHAR(50) = NULL
	--DECLARE @OverridePartnerFac NVARCHAR(50) = NULL
	--DECLARE @OverridePartnerTeam NVARCHAR(50) = NULL
	--DECLARE @OverridePartnerCostCentre NVARCHAR(50) = NULL
	--DECLARE @AnonymiseData BIT = 0

	--Get date of new ILR Return to be imported
	IF @AcademicYear >= '19/20'
	BEGIN
		SET @SQLString = N'
			SELECT
				@FileDateOUT = SRC.DateTime
			FROM ' + @FISDatabase + '.Valid.Source SRC';
	END
	ELSE
	BEGIN
		SET @SQLString = N'
			SELECT
				@FileDateOUT = SRC.DateTime
			FROM ' + @FISDatabase + '.dbo.Valid_Source SRC';
	END

    SET @SQLParams = 
        N'@FileDateOUT DATETIME OUTPUT';

    EXECUTE sp_executesql 
        @SQLString, 
        @SQLParams, 
        @FileDateOUT = @FileDate OUTPUT;

	IF @Mode = 'R'
		--If mode is to replace then drop the table
		IF OBJECT_ID('dbo.FIS_FundingData', 'U') IS NOT NULL 
			BEGIN
				SET @SQLString = N'
					DROP TABLE dbo.FIS_FundingData'

                SET @SQLParams = N'';

				EXECUTE sp_executesql 
                    @SQLString, 
                    @SQLParams;
			END
	ELSE
		--Confirm table exists already
		IF OBJECT_ID('dbo.FIS_FundingData', 'U') IS NULL 
			SET @TableExists = 0;

	IF @Mode = 'R' OR @TableExists = 0
		--If mode is to replace or table does not exist then create it
		BEGIN
			SET @SQLString = N'
				CREATE TABLE ' + @FISOutputTableLocation + 'FIS_FundingData (
					AcademicYear NVARCHAR(5) NULL,
                    ProviderNumber INT NULL,
					ProviderName NVARCHAR(50) NULL,
					ILRReturn NVARCHAR(3) NULL,
					IsFinalReturn INT NULL,
					ILRReturnDate DATETIME NULL,
					SQLDatabase NVARCHAR(50) NULL,
					ImportDate DATETIME NULL,
					Is1619FundingSplit INT NULL,
					ALS1619FundingPercent DECIMAL(5, 2) NULL,
					Is1619FundingReapportionedByAim INT NULL,
					InvalidLearners INT NULL,

					LearnerRef NVARCHAR(12) NULL,
					Surname NVARCHAR(100) NULL,
					Forename NVARCHAR(100) NULL,
					Sex NVARCHAR(1) NULL,
					DOB DATETIME NULL,
					Age31Aug INT NULL,
					ULN BIGINT NULL,
					Address1 NVARCHAR(50) NULL,
					Address2 NVARCHAR(50) NULL,
					Address3 NVARCHAR(50) NULL,
					Address4 NVARCHAR(50) NULL,
					PostCodeCurrent NVARCHAR(10) NULL,
					PostCodePriorToEnrol NVARCHAR(8) NULL,
					PostCodeLearningStartDate NVARCHAR(8) NULL,
					Tel NVARCHAR(18) NULL,
					Email NVARCHAR(100) NULL,
					PostCodeWardCode NVARCHAR(10) NULL,
					PostCodeWardName NVARCHAR(100) NULL,
					PostCodeDistrictCode NVARCHAR(4) NULL,
					PostCodeDistrictName NVARCHAR(50) NULL,
					PostCodeLEACode NVARCHAR(3) NULL,
					PostCodeLEAName NVARCHAR(50) NULL,
					PostCode1619Uplift DECIMAL(10, 5) NULL,
					PostCodeAdultUplift DECIMAL(10, 5) NULL,
					PostCodeAppUplift DECIMAL(10, 5) NULL,
					PostCodeUpliftApplied DECIMAL(10, 5) NULL,
					EmpStatusCode INT NULL,
					EmpStatusName NVARCHAR(20) NULL,
					EmpStatusAppliesDate DATETIME NULL,
					EmpStatusEmployerID INT NULL,
					LengthOfEmploy INT NULL,
					LengthOfUnemploy INT NULL,
					BenefitStatusInd INT NULL,
					SmallEmployer INT NULL,
					SelfEmployInd INT NULL,
					EmployIntensityInd INT NULL,
					IsFirstFullLevel2 INT NULL,
					IsFirstFullLevel3 INT NULL,
					EthnicityCode INT NULL,
					EthnicityName NVARCHAR(62) NULL,
					PrimaryLLDDHealthProblemCode INT NULL,
					PrimaryLLDDHealthProblemName NVARCHAR(100) NULL,
					DestinationProgressionType NVARCHAR(3) NULL,
					DestinationProgressionNumber INT NULL,
					DestinationProgressionCode NVARCHAR(10) NULL,
					DestinationProgressionName NVARCHAR(255) NULL,
					DestinationProgressionStartDate DATETIME NULL,
					DestinationProgressionEndDate DATETIME NULL,
					DestinationProgressionCollectionDate DATETIME NULL,
	'
    
    SET @SQLString += 
        N'
					SoftwareSupplierAimID NVARCHAR(36) NULL,
					FundLine NVARCHAR(100) NULL,
					FundLineSummary NVARCHAR(100) NULL,
                    FundLineDetail NVARCHAR(100) NULL,
					FundModel INT NULL,
					HEFullPartTime NVARCHAR(50) NULL,
					SortOrder INT NULL,
					IsFunded INT NULL,
					IsPre42DayWdrAllYr INT NULL,
					IsXfrAllYr INT NULL,
					IsStartAllYr INT NULL,
					IsPost42DayWdrAllYr INT NULL,
					IsCompAllYr INT NULL,
					IsAchAllYr INT NULL,
					IsPre42DayWdrCurYr INT NULL,
					IsXfrCurYr INT NULL,
					IsStartCurYr INT NULL,
					IsPost42DayWdrCurYr INT NULL,
					IsCompCurYr INT NULL,
					IsAchCurYr INT NULL,
					IsHighGrade INT NULL,

					IsMainAim INT NULL,
					IsMainAimInFundModel INT NULL,
					IsAdvLearnLoan INT NULL,
					IsAdvLearnLoanBursary INT NULL,
					RateBand NVARCHAR(50) NULL,
					RateBandOrder INT NULL,
					GCSEMathsGrade NVARCHAR(4) NULL,
					GCSEEnglishGrade NVARCHAR(4) NULL,
					CofMaths NVARCHAR(100) NULL,
					CofEnglish NVARCHAR(100) NULL,
					CofMathsMet INT NULL,
					CofEnglishMet INT NULL,
					CofMet INT NULL,
	'
    
    SET @SQLString += 
        N'
					PostCodeDelivery NVARCHAR(10) NULL,
					CampusID NVARCHAR(20) NULL,
					ParentCollegeCode NVARCHAR(20) NULL,
					ParentCollegeName NVARCHAR(150) NULL,
					ParentSiteCode NVARCHAR(20) NULL,
					ParentSiteName NVARCHAR(100) NULL,
					ParentFacCode NVARCHAR(20) NULL,
					ParentFacName NVARCHAR(150) NULL,
					ParentTeamCode NVARCHAR(20) NULL,
					ParentTeamName NVARCHAR(150) NULL,
					ParentCostCentre NVARCHAR(20) NULL,
					CollegeCode NVARCHAR(20) NULL,
					CollegeName NVARCHAR(150) NULL,
					SiteCode NVARCHAR(20) NULL,
					SiteName NVARCHAR(100) NULL,
					FacCode NVARCHAR(20) NULL,
					FacName NVARCHAR(150) NULL,
					TeamCode NVARCHAR(20) NULL,
					TeamName NVARCHAR(150) NULL,
					CostCentre NVARCHAR(20) NULL,
					MainFacCode NVARCHAR(20) NULL,
					MainFacName NVARCHAR(150) NULL,
					MainTeamCode NVARCHAR(20) NULL,
					MainTeamName NVARCHAR(150) NULL,
					FacCodeOrig NVARCHAR(20) NULL,
					FacNameOrig NVARCHAR(150) NULL,
					TeamCodeOrig NVARCHAR(20) NULL,
					TeamNameOrig NVARCHAR(150) NULL,
					CostCentreOrig NVARCHAR(20) NULL,
					SSA1Code NVARCHAR(5) NULL,
					SSA1Title NVARCHAR(255) NULL,
					SSA2Code NVARCHAR(5) NULL,
					SSA2Title NVARCHAR(255) NULL,
					CourseCode NVARCHAR(20) NULL,
					CourseTitle NVARCHAR(255) NULL,

					AimSequence INT NULL,
					AimType INT NULL,
					AimCode NVARCHAR(8) NULL,
					AimTitle NVARCHAR(254) NULL,
					AimTypeCode NVARCHAR(4) NULL,
					StartDate DATETIME NULL,
					ExpEndDate DATETIME NULL,
					ActEndDate DATETIME NULL,
					LearnerPLH INT NULL,
					LearnerEEP INT NULL,
					FM25PLHAim INT NULL,
					FM25EEPAim INT NULL,
					FM25PLHAllAims INT NULL,
					FM25EEPAllAims INT NULL,

					CompletionCode INT NULL,
					CompletionDesc NVARCHAR(20) NULL,
					OutcomeCode INT NULL,
					OutcomeDesc NVARCHAR(20) NULL,
					WithdrawReason NVARCHAR(2) NULL,
					Grade NVARCHAR(6) NULL,
					AwardBody NVARCHAR(20) NULL,

					ProgType INT NULL,
					StandardCode INT NULL,
					StandardName NVARCHAR(750) NULL,
					FrameworkCode INT NULL,
					FrameworkName NVARCHAR(150) NULL,
					PathwayCode INT NULL,
					PathwayName NVARCHAR(150) NULL,
					OffTheJobActualHours INT NULL,
					EmployerID INT NULL,
					EmployerName NVARCHAR(100) NULL,
					PartnerCode INT NULL,
					PartnerName NVARCHAR(100) NULL,
					
					AimValue DECIMAL(10, 5) NULL,
					ProgWeightFactor NCHAR(1) NULL,
					UnweightFundRate DECIMAL(10, 5) NULL,
					WeightFundRate DECIMAL(10, 5) NULL,
					NVQLevel NCHAR(1) NULL,
					CoFundingIndicator NVARCHAR(10) NULL,
					IsCarryIn INT NULL,
					HEQualEnt3 NVARCHAR(3) NULL,
					HESoc2000 INT NULL,
					HESec INT NULL,
					HEUCASAppID NVARCHAR(9) NULL,
					HETypeYr INT NULL,
					HEModeStu INT NULL,
					HEFundLev INT NULL,
					HEFundComp INT NULL,
					HEStuLoad DECIMAL(4, 1) NULL,
					HEYearStu INT NULL,
					HEMajorSourceStuFee INT NULL,
					HEPercentageNotTaught DECIMAL(4, 1) NULL,
					HEPercentTaughtFirstLDCS DECIMAL(4, 1) NULL,
					HEPercentTaughtSecondLDCS DECIMAL(4, 1) NULL,
					HEPercentTaughtThirdLDCS DECIMAL(4, 1) NULL,
					HESpecialFeeIndicator INT NULL,
					HENetTuitionFee INT NULL,
					HEGrossTuitionFee INT NULL,
					HEDomicile NVARCHAR(2) NULL,
					HEELQ INT NULL,
					HEPostCode NVARCHAR(8) NULL,
					EnrolmentID NVARCHAR(20) NULL,
					FM25TotFund DECIMAL(10, 5) NULL,
	'
    
    SET @SQLString += 
        N'
					NumPlannedOnProgPayments INT NULL,
					NumOutstandingOnProgPayments INT NULL,
					AchieveElement DECIMAL(10, 5) NULL,
					NonGovCont DECIMAL(10, 5) NULL,
					PropFundRemain DECIMAL(10, 5) NULL,
					PropFundRemainAch DECIMAL(10, 5) NULL,
					CarryInOnProg DECIMAL(10, 5) NULL,
					CarryInYr1R01 DECIMAL(10, 5) NULL,
					CarryInYr1R02 DECIMAL(10, 5) NULL,
					CarryInYr1R03 DECIMAL(10, 5) NULL,
					CarryInYr1R04 DECIMAL(10, 5) NULL,
					CarryInYr1R05 DECIMAL(10, 5) NULL,
					CarryInYr1R06 DECIMAL(10, 5) NULL,
					CarryInYr1R07 DECIMAL(10, 5) NULL,
					CarryInYr1R08 DECIMAL(10, 5) NULL,
					CarryInYr1R09 DECIMAL(10, 5) NULL,
					CarryInYr1R10 DECIMAL(10, 5) NULL,
					CarryInYr1R11 DECIMAL(10, 5) NULL,
					CarryInYr1R12 DECIMAL(10, 5) NULL,
					CarryInYr2R01 DECIMAL(10, 5) NULL,
					CarryInYr2R02 DECIMAL(10, 5) NULL,
					CarryInYr2R03 DECIMAL(10, 5) NULL,
					CarryInYr2R04 DECIMAL(10, 5) NULL,
					CarryInYr2R05 DECIMAL(10, 5) NULL,
					CarryInYr2R06 DECIMAL(10, 5) NULL,
					CarryInYr2R07 DECIMAL(10, 5) NULL,
					CarryInYr2R08 DECIMAL(10, 5) NULL,
					CarryInYr2R09 DECIMAL(10, 5) NULL,
					CarryInYr2R10 DECIMAL(10, 5) NULL,
					CarryInYr2R11 DECIMAL(10, 5) NULL,
					CarryInYr2R12 DECIMAL(10, 5) NULL,
    '
    
    SET @SQLString += 
        N'
					OnProgPaymentToPeriod DECIMAL(10, 5) NULL,
					LearnSuppPaymentToPeriod DECIMAL(10, 5) NULL,
					AchCompPaymentToPeriod DECIMAL(10, 5) NULL,
					BalancePaymentToPeriod DECIMAL(10, 5) NULL,
					EmpOutcomePayToPeriod DECIMAL(10, 5) NULL,
					OtherPaymentToPeriod DECIMAL(10, 5) NULL,
					TotFundToPeriod DECIMAL(10, 5) NULL,

					OnProgPayment6Mon DECIMAL(10, 5) NULL,
					LearnSuppPayment6Mon DECIMAL(10, 5) NULL,
					AchCompPayment6Mon DECIMAL(10, 5) NULL,
					BalancePayment6Mon DECIMAL(10, 5) NULL,
					EmpOutcomePay6Mon DECIMAL(10, 5) NULL,
					OtherPayment6Mon DECIMAL(10, 5) NULL,
					TotFund6Mon DECIMAL(10, 5) NULL,

					OnProgPaymentYrEnd DECIMAL(10, 5) NULL,
					LearnSuppPaymentYrEnd DECIMAL(10, 5) NULL,
					AchCompPaymentYrEnd DECIMAL(10, 5) NULL,
					BalancePaymentYrEnd DECIMAL(10, 5) NULL,
					EmpOutcomePayYrEnd DECIMAL(10, 5) NULL,
					OtherPaymentYrEnd DECIMAL(10, 5) NULL,
					TotFundYrEnd DECIMAL(10, 5) NULL,
					TotFundYrEndNotApportioned DECIMAL(10, 5) NULL,

					FutureAchievePayment DECIMAL(10, 5) NULL,
					FutureAchieveWeighting DECIMAL(5, 2) NULL,
					FutureAchievePaymentWeighted DECIMAL(10, 5) NULL,

					AppStandardPriceEpisodeContractType NVARCHAR(50) NULL,
					AppStandardPriceEpisodeStartDate DATE NULL,
					AppStandardPriceEpisodeCompletionElement DECIMAL (12, 5) NULL,
					AppStandardTotalNegotiatedPriceTNP DECIMAL (12, 5) NULL,
					AppStandardEmployerContibutionPMRTot DECIMAL (12, 5) NULL,
					AppStandardEmployerContibutionPMRCum DECIMAL (12, 5) NULL,

					OnProgPaymentP01 DECIMAL(10, 5) NULL,
					LearnSuppPaymentP01 DECIMAL(10, 5) NULL,
					AchCompPaymentP01 DECIMAL(10, 5) NULL,
					BalancePaymentP01 DECIMAL(10, 5) NULL,
					EmpOutcomePayP01 DECIMAL(10, 5) NULL,
					OtherPaymentP01 DECIMAL(10, 5) NULL,
					TotFundP01 DECIMAL(10, 5) NULL,

					OnProgPaymentP02 DECIMAL(10, 5) NULL,
					LearnSuppPaymentP02 DECIMAL(10, 5) NULL,
					AchCompPaymentP02 DECIMAL(10, 5) NULL,
					BalancePaymentP02 DECIMAL(10, 5) NULL,
					EmpOutcomePayP02 DECIMAL(10, 5) NULL,
					OtherPaymentP02 DECIMAL(10, 5) NULL,
					TotFundP02 DECIMAL(10, 5) NULL,

					OnProgPaymentP03 DECIMAL(10, 5) NULL,
					LearnSuppPaymentP03 DECIMAL(10, 5) NULL,
					AchCompPaymentP03 DECIMAL(10, 5) NULL,
					BalancePaymentP03 DECIMAL(10, 5) NULL,
					EmpOutcomePayP03 DECIMAL(10, 5) NULL,
					OtherPaymentP03 DECIMAL(10, 5) NULL,
					TotFundP03 DECIMAL(10, 5) NULL,

					OnProgPaymentP04 DECIMAL(10, 5) NULL,
					LearnSuppPaymentP04 DECIMAL(10, 5) NULL,
					AchCompPaymentP04 DECIMAL(10, 5) NULL,
					BalancePaymentP04 DECIMAL(10, 5) NULL,
					EmpOutcomePayP04 DECIMAL(10, 5) NULL,
					OtherPaymentP04 DECIMAL(10, 5) NULL,
					TotFundP04 DECIMAL(10, 5) NULL,

					OnProgPaymentP05 DECIMAL(10, 5) NULL,
					LearnSuppPaymentP05 DECIMAL(10, 5) NULL,
					AchCompPaymentP05 DECIMAL(10, 5) NULL,
					BalancePaymentP05 DECIMAL(10, 5) NULL,
					EmpOutcomePayP05 DECIMAL(10, 5) NULL,
					OtherPaymentP05 DECIMAL(10, 5) NULL,
					TotFundP05 DECIMAL(10, 5) NULL,

					OnProgPaymentP06 DECIMAL(10, 5) NULL,
					LearnSuppPaymentP06 DECIMAL(10, 5) NULL,
					AchCompPaymentP06 DECIMAL(10, 5) NULL,
					BalancePaymentP06 DECIMAL(10, 5) NULL,
					EmpOutcomePayP06 DECIMAL(10, 5) NULL,
					OtherPaymentP06 DECIMAL(10, 5) NULL,
					TotFundP06 DECIMAL(10, 5) NULL,

					OnProgPaymentP07 DECIMAL(10, 5) NULL,
					LearnSuppPaymentP07 DECIMAL(10, 5) NULL,
					AchCompPaymentP07 DECIMAL(10, 5) NULL,
					BalancePaymentP07 DECIMAL(10, 5) NULL,
					EmpOutcomePayP07 DECIMAL(10, 5) NULL,
					OtherPaymentP07 DECIMAL(10, 5) NULL,
					TotFundP07 DECIMAL(10, 5) NULL,

					OnProgPaymentP08 DECIMAL(10, 5) NULL,
					LearnSuppPaymentP08 DECIMAL(10, 5) NULL,
					AchCompPaymentP08 DECIMAL(10, 5) NULL,
					BalancePaymentP08 DECIMAL(10, 5) NULL,
					EmpOutcomePayP08 DECIMAL(10, 5) NULL,
					OtherPaymentP08 DECIMAL(10, 5) NULL,
					TotFundP08 DECIMAL(10, 5) NULL,

					OnProgPaymentP09 DECIMAL(10, 5) NULL,
					LearnSuppPaymentP09 DECIMAL(10, 5) NULL,
					AchCompPaymentP09 DECIMAL(10, 5) NULL,
					BalancePaymentP09 DECIMAL(10, 5) NULL,
					EmpOutcomePayP09 DECIMAL(10, 5) NULL,
					OtherPaymentP09 DECIMAL(10, 5) NULL,
					TotFundP09 DECIMAL(10, 5) NULL,

					OnProgPaymentP10 DECIMAL(10, 5) NULL,
					LearnSuppPaymentP10 DECIMAL(10, 5) NULL,
					AchCompPaymentP10 DECIMAL(10, 5) NULL,
					BalancePaymentP10 DECIMAL(10, 5) NULL,
					EmpOutcomePayP10 DECIMAL(10, 5) NULL,
					OtherPaymentP10 DECIMAL(10, 5) NULL,
					TotFundP10 DECIMAL(10, 5) NULL,

					OnProgPaymentP11 DECIMAL(10, 5) NULL,
					LearnSuppPaymentP11 DECIMAL(10, 5) NULL,
					AchCompPaymentP11 DECIMAL(10, 5) NULL,
					BalancePaymentP11 DECIMAL(10, 5) NULL,
					EmpOutcomePayP11 DECIMAL(10, 5) NULL,
					OtherPaymentP11 DECIMAL(10, 5) NULL,
					TotFundP11 DECIMAL(10, 5) NULL,

					OnProgPaymentP12 DECIMAL(10, 5) NULL,
					LearnSuppPaymentP12 DECIMAL(10, 5) NULL,
					AchCompPaymentP12 DECIMAL(10, 5) NULL,
					BalancePaymentP12 DECIMAL(10, 5) NULL,
					EmpOutcomePayP12 DECIMAL(10, 5) NULL,
					OtherPaymentP12 DECIMAL(10, 5) NULL,
					TotFundP12 DECIMAL(10, 5) NULL
				)';

			SET @SQLParams = N'';

            EXECUTE sp_executesql 
                @SQLString, 
                @SQLParams;

			SET @SQLString = N'
				CREATE CLUSTERED INDEX CX_FIS_FundingData ON ' + @FISOutputTableLocation + 'FIS_FundingData ( AcademicYear, ILRReturn, ILRReturnDate )';

			SET @SQLParams = N'';

            EXECUTE sp_executesql 
                @SQLString, 
                @SQLParams;
		END
	ELSE
		--If mode is to insert instead of replace then see if this ILR has already been imported
		BEGIN
			SET @SQLString = N'
				SELECT
					@NumExistingRecordsOUT = SUM ( 1 )
				FROM ' + @FISOutputTableLocation + 'FIS_FundingData FD
				WHERE
					FD.AcademicYear = @AcademicYear
					AND FD.ILRReturnDate = @FileDate';

			SET @SQLParams = 
				N'@AcademicYear NVARCHAR(5),
				@FileDate DATETIME,
				@NumExistingRecordsOUT INT OUTPUT';

			EXECUTE sp_executesql 
				@SQLString, 
				@SQLParams, 
				@AcademicYear = @AcademicYear, 
				@FileDate = @FileDate,
				@NumExistingRecordsOUT = @NumExistingRecords OUTPUT;

			IF @NumExistingRecords > 0
				--If same ILR has already been imported then delete it
				BEGIN
                    SET @SQLString = N'
                        DELETE FD
						FROM ' + @FISOutputTableLocation + 'FIS_FundingData FD
						WHERE
							FD.AcademicYear = @AcademicYear
							AND FD.ILRReturnDate = @FileDate';

                    SET @SQLParams = 
						N'@AcademicYear NVARCHAR(5),
						@FileDate DATETIME';

                    EXECUTE sp_executesql 
                        @SQLString, 
                        @SQLParams,
						@AcademicYear = @AcademicYear,
						@FileDate = @FileDate;
				END
		END

	--Finally insert new data into table
	SET @SQLString = N'
		EXEC SPR_FIS_FundingData_' + REPLACE ( @AcademicYear, '/', '' ) + '
			@FISDatabase,
			@AcademicYear,
			@ILRReturn,
			@IsFinalReturn,
			@Split1619Funding,
			@Reapportion1619FundingByAimWeighting,
            @1619ALSFundingPercent,
			@IncludeHEAdvLoanPossIncome,
			@IncludeAdvLoanBursaryIncome,
			@FutureAchieveWeighting,
            @FISOutputTableLocation,
			@ProviderFieldFacLocation,
			@ProviderFieldTeamLocation,
			@ProviderFieldCostCentreLocation,
			@ProviderFieldCourseLocation,
			@ProviderFieldParentCourseLocation,
			@OverrideEngFac,
			@OverrideMatFac,
			@OverrideEngTeam,
			@OverrideMatTeam,
			@OverrideEngCostCentre,
			@OverrideMatCostCentre,
			@OverridePartnerFac,
			@OverridePartnerTeam,
			@OverridePartnerCostCentre';

    SET @SQLParams = 
        N'@FISDatabase NVARCHAR(50),
        @AcademicYear NVARCHAR(5),
    	@ILRReturn INT,
    	@IsFinalReturn BIT,
    	@Split1619Funding BIT,
		@Reapportion1619FundingByAimWeighting BIT,
        @1619ALSFundingPercent DECIMAL(5,2),
		@IncludeHEAdvLoanPossIncome BIT,
		@IncludeAdvLoanBursaryIncome BIT,
    	@FutureAchieveWeighting DECIMAL(5,2),
        @FISOutputTableLocation NVARCHAR(200),
    	@ProviderFieldFacLocation CHAR(1),
		@ProviderFieldTeamLocation CHAR(1),
		@ProviderFieldCostCentreLocation CHAR(1),
		@ProviderFieldCourseLocation CHAR(1),
		@ProviderFieldParentCourseLocation CHAR(1),
    	@OverrideEngFac NVARCHAR(50),
    	@OverrideMatFac NVARCHAR(50),
    	@OverrideEngTeam NVARCHAR(50),
    	@OverrideMatTeam NVARCHAR(50),
    	@OverrideEngCostCentre NVARCHAR(50),
    	@OverrideMatCostCentre NVARCHAR(50),
    	@OverridePartnerFac NVARCHAR(50),
    	@OverridePartnerTeam NVARCHAR(50),
    	@OverridePartnerCostCentre NVARCHAR(50)';
    
	--SELECT @SQLString AS [processing-instruction(x)] FOR XML PATH('')

    EXECUTE sp_executesql 
        @SQLString, 
        @SQLParams, 
        @FISDatabase = @FISDatabase,
        @AcademicYear = @AcademicYear,
    	@ILRReturn = @ILRReturn,
    	@IsFinalReturn = @IsFinalReturn,
    	@Split1619Funding = @Split1619Funding,
		@Reapportion1619FundingByAimWeighting = @Reapportion1619FundingByAimWeighting,
        @1619ALSFundingPercent = @1619ALSFundingPercent,
		@IncludeHEAdvLoanPossIncome = @IncludeHEAdvLoanPossIncome,
		@IncludeAdvLoanBursaryIncome = @IncludeAdvLoanBursaryIncome,
    	@FutureAchieveWeighting = @FutureAchieveWeighting,
        @FISOutputTableLocation = @FISOutputTableLocation,
    	@ProviderFieldFacLocation = @ProviderFieldFacLocation,
		@ProviderFieldTeamLocation = @ProviderFieldTeamLocation,
		@ProviderFieldCostCentreLocation = @ProviderFieldCostCentreLocation,
		@ProviderFieldCourseLocation = @ProviderFieldCourseLocation,
		@ProviderFieldParentCourseLocation = @ProviderFieldParentCourseLocation,
    	@OverrideEngFac = @OverrideEngFac,
    	@OverrideMatFac = @OverrideMatFac,
    	@OverrideEngTeam = @OverrideEngTeam,
    	@OverrideMatTeam = @OverrideMatTeam,
    	@OverrideEngCostCentre = @OverrideEngCostCentre,
    	@OverrideMatCostCentre = @OverrideMatCostCentre,
    	@OverridePartnerFac = @OverridePartnerFac,
    	@OverridePartnerTeam = @OverridePartnerTeam,
    	@OverridePartnerCostCentre = @OverridePartnerCostCentre;


	--Anonymise the ILR data if required
	IF @AnonymiseData = 1
	BEGIN
		DECLARE @AnonName VARCHAR(250) = 'Anonymous'
		DECLARE @AnonAddress VARCHAR(250) = 'Anonymous'
		DECLARE @AnonPostCode VARCHAR(250) = 'ZZ99 9ZZ'
		DECLARE @AnonTel VARCHAR(250) = '000000000000000'
		DECLARE @AnonEmail VARCHAR(250) = 'anon@anon.com'

		SET @SQLString = N'
			UPDATE FD
			SET
				Surname = CASE WHEN FD.Surname IS NOT NULL THEN @AnonName ELSE NULL END,
				Forename = CASE WHEN FD.Forename IS NOT NULL THEN @AnonName ELSE NULL END,
				Address1 = CASE WHEN FD.Address1 IS NOT NULL THEN @AnonAddress ELSE NULL END,
				Address2 = CASE WHEN FD.Address2 IS NOT NULL THEN @AnonAddress ELSE NULL END,
				Address3 = CASE WHEN FD.Address3 IS NOT NULL THEN @AnonAddress ELSE NULL END,
				Address4 = CASE WHEN FD.Address4 IS NOT NULL THEN @AnonAddress ELSE NULL END,
				PostCodeCurrent = CASE WHEN FD.PostCodeCurrent IS NOT NULL THEN @AnonPostCode ELSE NULL END,
				PostCodePriorToEnrol = CASE WHEN FD.PostCodePriorToEnrol IS NOT NULL THEN @AnonPostCode ELSE NULL END,
				PostCodeLearningStartDate = CASE WHEN FD.PostCodeLearningStartDate IS NOT NULL THEN @AnonPostCode ELSE NULL END,
				Tel = CASE WHEN FD.Tel IS NOT NULL THEN @AnonTel ELSE NULL END,
				Email = CASE WHEN FD.Email IS NOT NULL THEN @AnonEmail ELSE NULL END,
				PostCodeDelivery = CASE WHEN FD.PostCodeDelivery IS NOT NULL THEN @AnonPostCode ELSE NULL END
			FROM ' + @FISOutputTableLocation + 'FIS_FundingData FD
			WHERE
				FD.AcademicYear = @AcademicYear
				AND FD.ILRReturnDate = @FileDate';

		SET @SQLParams = 
			N'@AcademicYear NVARCHAR(5),
			@FileDate DATETIME,
			@AnonName VARCHAR(250),
			@AnonAddress VARCHAR(250),
			@AnonPostCode VARCHAR(250),
			@AnonTel VARCHAR(250),
			@AnonEmail VARCHAR(250)';

		EXECUTE sp_executesql 
			@SQLString, 
			@SQLParams,
			@AcademicYear = @AcademicYear,
			@FileDate = @FileDate,
			@AnonName = @AnonName,
			@AnonAddress = @AnonAddress,
			@AnonPostCode = @AnonPostCode,
			@AnonTel = @AnonTel,
			@AnonEmail = @AnonEmail;
	END
	
END