DECLARE @CollegeRef NVARCHAR(250) = 'Wigan'
DECLARE @AcademicYear NVARCHAR(5) = '21/22'
DECLARE @ILRReturn NVARCHAR(3) = 'R14'
DECLARE @IsFinalReturn BIT = 1
DECLARE @NewDatabasePath NVARCHAR(500) = 'D:\MSSQL\Data\'

--Set database offline, move files to new location, run rest of script (or just run script twice but will get errors until files are moved)


DECLARE @DatabaseName NVARCHAR(500) = NULL
DECLARE @DataFileName NVARCHAR(500) = NULL
DECLARE @LogFileName NVARCHAR(500) = NULL
DECLARE @MDFPath NVARCHAR(500) = NULL
DECLARE @LDFPath NVARCHAR(500) = NULL

DECLARE @SQL NVARCHAR(MAX) = NULL


SET @DatabaseName = 
	+ 'ILR' 
	+ REPLACE ( @AcademicYear, '/', '' ) 
	+ '_' + @ILRReturn 
	+ CASE
		WHEN @IsFinalReturn = 1 THEN 'F'
		ELSE ''
	END
	+ '_' + @CollegeRef


SET @SQL =
	N'ALTER DATABASE ' + @DatabaseName + ' SET OFFLINE;'

	
--PRINT @SQL
EXEC sp_executesql @SQL

--NOW MOVE DATA FILES TO NEW LOCATION


--Get Data File Names for this database
DROP TABLE IF EXISTS #DataFileNames
SELECT
	DataFileName = MF.name,
	DataFilePhysicalName = 
		CASE
			WHEN CHARINDEX ( '\', REVERSE ( MF.physical_name ) ) > 0 THEN
				RIGHT ( MF.physical_name, CHARINDEX ( '\', REVERSE ( MF.physical_name ) ) - 1 )
			ELSE MF.physical_name
		END,
	DataFileCurrentPath = MF.physical_name,
	LogFileName = MFL.name,
	LogFilePhysicalName = 
		CASE
			WHEN CHARINDEX ( '\', REVERSE ( MFL.physical_name ) ) > 0 THEN
				RIGHT ( MFL.physical_name, CHARINDEX ( '\', REVERSE ( MFL.physical_name ) ) - 1 )
			ELSE MFL.physical_name
		END,
	LogFileCurrentPath = MFL.physical_name
	INTO #DataFileNames
FROM sys.master_files MF
INNER JOIN sys.master_files MFL
	ON MFL.database_id = MF.database_id
	AND MFL.type = 1 --Log
	AND MFL.file_id = 2 --Only made to support databases with 1 data file and 1 log
WHERE
	MF.database_id = DB_ID(@DatabaseName)
	AND MF.type = 0 --Rows
	AND MF.file_id = 1


SELECT
	@DataFileName = DF.DataFileName,
	@LogFileName = DF.LogFileName,
	@MDFPath = 
		@NewDatabasePath 
		+ DF.DataFilePhysicalName,
	@LDFPath = 
		@NewDatabasePath 
		+ DF.LogFilePhysicalName
FROM #DataFileNames DF


SET @SQL =
	N'ALTER DATABASE [' + @DatabaseName + ']
		MODIFY FILE ( 
			NAME = ' + @DataFileName + ',
			FILENAME = ''' + @MDFPath + '''
		);'

--PRINT @SQL
EXEC sp_executesql @SQL


SET @SQL =
	N'ALTER DATABASE [' + @DatabaseName + ']
		MODIFY FILE ( 
			NAME = ' + @LogFileName + ',
			FILENAME = ''' + @LDFPath + '''
		);'

--PRINT @SQL
EXEC sp_executesql @SQL

SET @SQL =
	N'ALTER DATABASE ' + @DatabaseName + ' SET ONLINE;'
	
--PRINT @SQL
EXEC sp_executesql @SQL


--Establish names of data files in case they are different
--SELECT name, physical_name AS NewLocation, state_desc AS OnlineStatus, *
--FROM sys.master_files  
--WHERE database_id = DB_ID(N'ILR2223_R14F_LancsAndMorecambe')

--SELECT *
--FROM sys.master_files MF
--ORDER BY
--	MF.physical_name