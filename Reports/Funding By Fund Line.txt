DECLARE @AcademicYear NVARCHAR(5) = '25/26'
DECLARE @ILRReturn NVARCHAR(3) = 'R05'
DECLARE @IsFinalReturn BIT = 1


SELECT
	FD.FundLineCategoryOrder,
	FD.FundLineCategory,
	FD.FundLineSubCategoryOrder,
	FundLineSubCategory,
	FD.FundLine,
	AllLearners = SUM ( FD.IsMainAimInFundModel ),
	LearnersLost = SUM ( CASE WHEN FD.IsMainAimInFundModel = 1 AND FD.IsOverallWithdrawnPreEligibility = 1 THEN 1 ELSE 0 END ),
	FundedLearners = SUM ( CASE WHEN FD.IsMainAimInFundModel = 1 AND FD.IsFundedStart = 1 THEN 1 ELSE 0 END ),
	Enrols = COUNT ( FD.LearnRefNumber ),
	WdrPre42 = SUM ( FD.IsOverallWithdrawnPreEligibility ),
	Starts = SUM ( FD.IsOverallStart ),
	Leavers = SUM ( FD.IsOverallLeaver ),
	BestCaseLeavers = SUM ( FD.IsOverallLeaverBestCase ),
	Retained = SUM ( FD.IsOverallRetainedFunded ),
	RetInYrPer = 
		ROUND (
			CASE
				WHEN SUM ( FD.IsOverallStart ) = 0 THEN 0
				ELSE
					CAST ( SUM ( FD.IsOverallRetainedFunded ) AS FLOAT )
					/
					CAST ( SUM ( FD.IsOverallStart ) AS FLOAT )
			END,
			3
		),
	RetPer = 
		ROUND (
			CASE
				WHEN SUM ( FD.IsOverallLeaver ) = 0 THEN 0
				ELSE
					CAST ( SUM ( FD.IsOverallCompletedFunded ) AS FLOAT )
					/
					CAST ( SUM ( FD.IsOverallLeaver ) AS FLOAT )
			END,
			3
		),
	WdrPost42 = SUM ( FD.IsOverallWithdrawnFunded ),
	BreakInLrn = SUM ( FD.IsOverallBreakInLearningFunded ),
	Completed = SUM ( FD.IsOverallCompletedFunded ),
	Achiever = SUM ( FD.IsOverallAchieverFunded ),
	Failed = SUM ( FD.IsOverallFailedFunded ),
	UnknownOutcome = SUM ( FD.IsOverallUnknownOutcomeFunded ),
	AchPer = 
		ROUND (
			CASE
				WHEN SUM ( FD.IsOverallLeaver ) = 0 THEN 0
				ELSE
					CAST ( SUM ( FD.IsOverallAchieverFunded ) AS FLOAT )
					/
					CAST ( SUM ( FD.IsOverallLeaver ) AS FLOAT )
			END,
			3
		),
	PassPer = 
		ROUND (
			CASE
				WHEN SUM ( FD.IsOverallCompletedFunded ) = 0 THEN 0
				ELSE
					CAST ( SUM ( FD.IsOverallAchieverFunded ) AS FLOAT )
					/
					CAST ( SUM ( FD.IsOverallCompletedFunded ) AS FLOAT )
			END,
			3
		),
	BestCaseAchiever = SUM ( FD.IsOverallAchieverBestCase ),
	TotalFunding = SUM ( FD.TotalEarnedCashYearEnd )
FROM FIS_FundingData FD
WHERE
	FD.AcademicYear = @AcademicYear
	AND FD.ILRReturn = @ILRReturn
	AND FD.IsFinalReturn = @IsFinalReturn
	--AND FD.IsFundedStart = 1
GROUP BY
	FD.FundLineCategoryOrder,
	FD.FundLineCategory,
	FD.FundLineSubCategoryOrder,
	FD.FundLineSubCategory,
	FD.FundLine
ORDER BY
	FD.FundLineCategoryOrder,
	FD.FundLineSubCategoryOrder,
	FD.FundLineSubCategory,
	FD.FundLine